' Gambas class file



Private $cookiesFile As String = ""

Private $proxyUrl As String = ""
Private $proxyPort As String = ""
Private $proxyUser As String = ""
Private $proxyPass As String = ""

Private $strUserAgent As String = "Lynx/2.8.9dev.6 libwww-FM/2.14 SSL-MM/1.4.1 OpenSSL/1.0.2h-fips"
'Private $strUserAgent As String = "Mozilla/5.0 (X11; Fedora; Linux; rv:47.0) Gecko/20100101 Firefox/47.0"
Private $strIniUrl As String = "https://translate.google.com/"
Private $strTranslateUrl As String = "https://translate.google.com/?hl=es#es/en/Hello%20my%20world."
Private $tOut As Integer = 5



Private $http As HttpForm ''As "GoogleHttp"
Private $swHttpON As Boolean = False
Private $htDoc As HTMDocument

Property Read sourceLang As String[]
Private $sourceLang As String[]
Property Read targetLang As String[]
Private $targetLang As String[]

 ''Google Translator FORM
 Private $frmFieldNameSourceText As String
 Private $frmFieldNameSourceLanguage As String
 Private $frmFieldNameTargetLanguage As String
 Private $frmFieldIDResult As String
 Private $frmFieldIDComboSourceLanguage As String
 Private $frmFieldIDComboTargetLanguage As String



Public Sub _new()
  loadSettings

  $cookiesFile = Temp$("cookies")	''/tmp/gambas.[id Usuario]*/*[id Proceso]*/*[Nombre].tmp

  $http = New HttpForm
  Object.Attach($http, Me, "GoogleHttp")
  
  $http.CookiesFile = $cookiesFile
  $http.UpdateCookies = True
  $http.UserAgent = $strUserAgent
  
  $http.Debug = True
  $http.Timeout = $tOut
End

Public Sub _free() ''***Se invoca al destruir el objeto
  saveSettings


  $htDoc.destroyDOM
  $htDoc = Null
  $http = Null
End

Private Function sourceLang_Read() As String[]
  If $sourceLang = Null Or If $sourceLang.Count < 1 Then
    updateGoogleData
    While $swHttpON = True
      Wait 0.1
    Wend
  Endif
  
  Return $sourceLang
End

Private Function targetLang_Read() As String[]
  If $targetLang = Null Or If $targetLang.Count < 1 Then
    updateGoogleData
    While $swHttpON = True
      Wait 0.1
    Wend
  Endif
  
  Return $targetLang
End

Private Sub loadSettings()
  $proxyUrl = Settings["Translators/Google/proxyUrl", ""]
  $proxyPort = Settings["Translators/Google/$proxyPort", ""]
  $proxyUser = Settings["Translators/Google/$proxyUser", ""]
  $proxyPass = Settings["Translators/Google/$proxyPass", ""]
  
  $tOut = Settings["Translators/Google/connectionTout", 5]
  $strUserAgent = Settings["Translators/Google/userAgent", "Lynx/2.8.9dev.6 libwww-FM/2.14 SSL-MM/1.4.1 OpenSSL/1.0.2h-fips"]
  $strIniUrl = Settings["Translators/Google/urlIni", "https://translate.google.com/"]
  $strTranslateUrl = Settings["Translators/Google/urlTranslate", "https://translate.google.com/?hl=es#es/en/Hello%20my%20world."]
  
  $sourceLang = Settings["Translators/Google/sourceLanguages", []]
  $targetLang = Settings["Translators/Google/targetLanguages", []]

   ''Google Translator FORM
   $frmFieldNameSourceText=Settings["Translators/Google/frmFieldNameSourceText", "text"]
   $frmFieldNameSourceLanguage=Settings["Translators/Google/frmFieldNameSourceLanguage", "sl"]
   $frmFieldNameTargetLanguage=Settings["Translators/Google/frmFieldNameTargetLanguage", "tl"]
   $frmFieldIDResult=Settings["Translators/Google/frmFieldIDResult", "result_box"]
   $frmFieldIDComboSourceLanguage=Settings["Translators/Google/frmFieldIDComboSourceLanguage", "gt-sl"]
   $frmFieldIDComboTargetLanguage=Settings["Translators/Google/frmFieldIDComboTargetLanguage", "gt-tl"]
End

Public Sub saveSettings()
   Settings["Translators/Google/proxyUrl"] = $proxyUrl
   Settings["Translators/Google/$proxyPort"] = $proxyPort
   Settings["Translators/Google/$proxyUser"] = $proxyUser
   Settings["Translators/Google/$proxyPass"] = $proxyPass
  
   Settings["Translators/Google/connectionTout"] = $tOut
   Settings["Translators/Google/userAgent"] = $strUserAgent
   Settings["Translators/Google/urlIni"] = $strIniUrl
   Settings["Translators/Google/urlTranslate"] = $strTranslateUrl
  
   Settings["Translators/Google/sourceLanguages"] = $sourceLang
   Settings["Translators/Google/targetLanguages"] = $targetLang
   
   ''Google Translator FORM
   Settings["Translators/Google/frmFieldNameSourceText"] = $frmFieldNameSourceText
   Settings["Translators/Google/frmFieldNameSourceLanguage"] = $frmFieldNameSourceLanguage
   Settings["Translators/Google/frmFieldNameTargetLanguage"] = $frmFieldNameTargetLanguage
   Settings["Translators/Google/frmFieldIDResult"] = $frmFieldIDResult	'"result_box"
   Settings["Translators/Google/frmFieldIDComboSourceLanguage"] = $frmFieldIDComboSourceLanguage	'"gt-sl"
   Settings["Translators/Google/frmFieldIDComboTargetLanguage"] = $frmFieldIDComboTargetLanguage			''"gt-tl"
  
  Settings.Save
End

Public Function translate(str As String, srcLang as String, trgtLang as String) As String 	''Translates the "str" from Source "srcLang" to Target "trgtLang".
  Dim strRet As string=""
  Dim htElm As HTMElement
  
  $swHttpON = True 	''Start the Request to Google.

  $http.URL = $strTranslateUrl
  
  $http.Add("js", "n")			'No JS.
  $http.Add("hl", "es")		''TODO: Get GUI Language
  $http.Add("ie", "UTF-8")	''TODO: Get GUI Language
  
  $http.Add($frmFieldNameSourceText, str)		'Text To translate.
  $http.Add($frmFieldNameSourceLanguage, srcLang)	'Translate from .....
  $http.Add($frmFieldNameTargetLanguage, trgtLang)	'Translate to ..... 
  
'~ type="hidden" id="gt-sl" name="sl" value="en"
'~ type="hidden" id="gt-tl" name="tl" value="es"
'~ type="submit" class="jfk-button jfk-button-action" tabindex="0" value="Traducir" id="gt-submit"
'~ type="hidden" id="js" value="y" name="js"
'~ type="hidden" id="prev" value="_t" name="prev"
'~ type="hidden" id="hl" value="es" name="hl"
'~ type="hidden" value="UTF-8" name="ie"
  
  $http.Submit
  
  While $swHttpON = True	''Wait to Http Finished ...
    Wait 0.1
  Wend
  
  If $http.code = 200 Then
    htElm = $htDoc.getElementById($frmFieldIDResult) ''Resultado de la traducción.
	If htElm <> Null Then
	  strRet=htElm.innerTEXT
	EndIf
  EndIf
  
  $http.Clear
  htElm = Null

  Return strRet
End


Public Function updateGoogleData() 	''Retrive Source and Target Languages.
  Dim htElm As HTMElement, arrHtElm As HTMElement[]
  
  $swHttpON = True 	''Start the Request to Google.

  $http.URL = $strIniUrl
  $http.Submit
  
  While $swHttpON = True	''Wait to Http Finished ...
    Wait 0.1
  Wend
  
  If $http.code = 200 Then
    ''SOURCE LANGUAGES (gt-sl)
    htElm = $htDoc.getElementById($frmFieldIDComboSourceLanguage)
    If htElm <> Null Then
      arrHtElm = htElm.childNodes
      For Each htElm In arrHtElm
        If htElm.attributes["value"] <> "auto" And If htElm.attributes["value"] <> "separator" Then
          $sourceLang.Add(htElm.attributes["value"] & "\t- " & htElm.firstChild.nodeValue)
        Endif
      Next
    Endif

    ''TARGET LANGUAGES (gt-tl)
    htElm = $htDoc.getElementById($frmFieldIDComboTargetLanguage)
    If htElm <> Null Then
      arrHtElm = htElm.childNodes
      For Each htElm In arrHtElm
        If htElm.attributes["value"] <> "auto" And If htElm.attributes["value"] <> "separator" Then
          $targetLang.Add(htElm.attributes["value"] & "\t- " & htElm.firstChild.nodeValue)
        Endif
      Next
    Endif
  Else
      Debug "ERROR en la conexion HTTP. Code: " & $http.Code & gb.CrLf & "Razon: " & $http.Reason
  Endif
  
  $http.Clear
  htElm = Null
  arrHtElm.Clear
  arrHtElm = Null
End

Private Sub GoogleHttp_Finished()
  Dim sline As String = "", sTotLines As String = ""
  
  If $http.code = 200 Then
    For Each sLine In $http.Lines
      sTotLines &= sLine
    Next
    
    $htDoc = New HTMDocument(sTotLines)
    '$htDoc.objectStats(True)
  Else
      Debug "ERROR en la conexion HTTP. Code: " & $http.Code & gb.CrLf & "Razon: " & $http.Reason
  Endif
  
  $swHttpON = False 	''Finish the Request to Google..
End

Private Sub GoogleHttp_Cancel()
  $swHttpON = False
End
' Private Sub GoogleHttp_Connect()
'   
' End
Private Sub GoogleHttp_Error()
  Debug "ERROR en la conexion HTTP. Code: " & $http.Code & gb.CrLf & "Razon: " & $http.Reason
  $swHttpON = False
End
' Private Sub GoogleHttp_Progress()
'   
' End  
' Private Sub GoogleHttp_Read()
'   
' End

